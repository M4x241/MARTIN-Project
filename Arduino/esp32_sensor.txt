#include <Arduino.h>

// =========================================
// PINES
// =========================================
#define TRIG_L 15
#define ECHO_L 2
#define TRIG_C 23
#define ECHO_C 22
#define TRIG_R 26
#define ECHO_R 25

const int ENA = 4;   
const int ENB = 19;  
const int IN1 = 16;  
const int IN2 = 17;
const int IN3 = 5;   
const int IN4 = 18;

const int PWM_FREQ = 20000;
const int PWM_RES = 8;

const int DISTANCIA_OBSTACULO = 10;
int velocidadBase = 200;

// ============================
// DURACIÓN DEL PASO DISCRETO
// ============================
const int STEP_TIME = 200;   // ms → duración del movimiento
const int SENSOR_INTERVAL = 120; // tiempo entre lecturas

// =========================================
// DISTANCIA
// =========================================
long medirDistancia(int trig, int echo) {
  digitalWrite(trig, LOW);
  delayMicroseconds(4);
  digitalWrite(trig, HIGH);
  delayMicroseconds(10);
  digitalWrite(trig, LOW);
  long duracion = pulseIn(echo, HIGH, 30000);
  if (duracion == 0) return -1;
  return duracion / 58;
}

// =========================================
// MOTORES
// =========================================
void detenerMotorIzquierdo() {
  digitalWrite(IN1, LOW); digitalWrite(IN2, LOW);
  ledcWrite(ENA, 0);
}

void detenerMotorDerecho() {
  digitalWrite(IN3, LOW); digitalWrite(IN4, LOW);
  ledcWrite(ENB, 0);
}

void motor_derecho_adelante(int speed) {
  digitalWrite(IN3, HIGH);
  digitalWrite(IN4, LOW);
  ledcWrite(ENB, speed);
}

void motor_derecho_atras(int speed) {
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, HIGH);
  ledcWrite(ENB, speed);
}

void motor_izquierdo_adelante(int speed) {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, HIGH);
  ledcWrite(ENA, speed);
}

void motor_izquierdo_atras(int speed) {
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
  ledcWrite(ENA, speed);
}

void detener() {
  detenerMotorIzquierdo();
  detenerMotorDerecho();
}

// =========================================
// ACCIONES DISCRETAS
// =========================================
const float FACTOR_IZQUIERDO = 1.25;
void accion_avanzar() {
  motor_derecho_adelante(velocidadBase);
  motor_izquierdo_adelante(velocidadBase);
}

void accion_retroceder() {
  motor_derecho_atras(velocidadBase);
  motor_izquierdo_atras(velocidadBase);
}

void accion_giro_izquierda() {
  motor_derecho_adelante(velocidadBase);
  detenerMotorIzquierdo();
}

void accion_giro_derecha() {
  detenerMotorDerecho();
  motor_izquierdo_adelante(velocidadBase);
}

// =========================================
// SETUP
// =========================================
void setup() {
  Serial.begin(115200);

  pinMode(IN1, OUTPUT); pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT); pinMode(IN4, OUTPUT);
  ledcAttach(ENA, PWM_FREQ, PWM_RES);
  ledcAttach(ENB, PWM_FREQ, PWM_RES);

  pinMode(TRIG_L, OUTPUT); pinMode(ECHO_L, INPUT);
  pinMode(TRIG_C, OUTPUT); pinMode(ECHO_C, INPUT);
  pinMode(TRIG_R, OUTPUT); pinMode(ECHO_R, INPUT);

  detener();
  Serial.println("ROBOT LISTO (acciones discretas)");
}

// =========================================
// LOOP
// =========================================
void loop() {

  // Esperar acción desde Python
  if (Serial.available()) {
    char cmd = Serial.read();

    // ======= ACCIÓN DISCRETA =======
    switch (cmd) {
      case '0': accion_avanzar(); break;
      case '1': accion_giro_izquierda(); break;
      case '2': accion_giro_derecha(); break;
      case '3': accion_retroceder(); break;
      default: detener(); break;
    }

    // Duración del paso
    delay(STEP_TIME);

    // Detener después del movimiento
    detener();

    // Pequeña pausa antes de enviar sensores
    delay(20);

    // ======= ENVIAR SENSORES =======
    long dL = medirDistancia(TRIG_L, ECHO_L);
    long dC = medirDistancia(TRIG_C, ECHO_C);
    long dR = medirDistancia(TRIG_R, ECHO_R);

    int L = (dL > 0 && dL <= DISTANCIA_OBSTACULO) ? 1 : 0;
    int C = (dC > 0 && dC <= DISTANCIA_OBSTACULO) ? 1 : 0;
    int R = (dR > 0 && dR <= DISTANCIA_OBSTACULO) ? 1 : 0;

    Serial.print("{\"L\":"); Serial.print(L);
    Serial.print(",\"C\":"); Serial.print(C);
    Serial.print(",\"R\":"); Serial.print(R);
    Serial.println("}");
  }
}
